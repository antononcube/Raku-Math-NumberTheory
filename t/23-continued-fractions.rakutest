use v6.d;
use Test;

#use lib <. lib>;
use Math::NumberTheory;

plan *;

## 1
subtest {

    isa-ok continued-fraction(12, 10), List:D;
    is continued-fraction(12, 10), (12,);
    isa-ok continued-fraction(415/93, 10), List:D;
    is-deeply continued-fraction(415/93, 10), (4, 2, 6, 7);
    is-deeply continued-fraction(π, 10), (3, 7, 15, 1, 292, 1, 1, 1, 2, 1);

}, 'Continued fraction with a second, number-of-terms argument';

## 2
subtest {
    isa-ok continued-fraction(12), List:D;
    is continued-fraction(12), (12,);
    isa-ok continued-fraction(415/93), List:D;
    is-deeply continued-fraction(415/93), (4, 2, 6, 7);

    # Negative number
    is-deeply continued-fraction(-415/93), (-4, -2, -6, -7);

}, 'Continued fraction with one arugment';

## 3
subtest {
    isa-ok continued-fraction([415/93, 232/31]), List:D;
    is continued-fraction([415/93, 232/31]).all ~~ List:D, True;
    is-deeply continued-fraction([415/93, 232/31]), ((4, 2, 6, 7), (7, 2, 15));
}, 'Listable';

## 4
subtest {

    isa-ok continued-fraction(12, tol => 0.1), List:D;
    is continued-fraction(12, tol => 0.1), (12,);
    isa-ok continued-fraction(415/93, tol => 0.1), List:D;
    is-deeply continued-fraction(415/93, tol => 0.1), (4, 2);

}, 'Continued fraction with tolerance';


## 5
subtest {

    # ContinuedFraction[\[Pi], 14]
    # Gives different last digit: (3, 7, 15, 1, 292, 1, 1, 1, 2, 1, 3, 1, 14, 2)
    is continued-fraction(π, 14), (3, 7, 15, 1, 292, 1, 1, 1, 2, 1, 3, 1, 14, 3);

    # ContinuedFraction[E, 14]
    is-deeply continued-fraction(e, 14), (2, 1, 2, 1, 1, 4, 1, 1, 6, 1, 1, 8, 1, 1);

}, 'Continued fraction vs Wolfram Language';

## 6
subtest {

    isa-ok from-continued-fraction((1, |(2 xx 9)), (1 xx 10)), Numeric:D;
    is from-continued-fraction((1, |(2 xx 9)), (1 xx 10)) - sqrt(2) ≤ 10e-6, True, 'sqrt(2) check';

    isa-ok from-continued-fraction([3, 7, 15, 1, 292, 1, 1, 1, 2, 1]), Numeric:D;
    is from-continued-fraction([3, 7, 15, 1, 292, 1, 1, 1, 2, 1]) - π ≤ 10e-6, True, 'π check';

    # Maybe a good idea, but can be hard to implement
    #is
    #        from-continued-fraction(3, 7, 15, 1, 292, 1, 1, 1, 2, 1),
    #        from-continued-fraction([3, 7, 15, 1, 292, 1, 1, 1, 2, 1]),
    #        'slurpy argument';

}, 'From continued fractions';

## 6
subtest {
    is-deeply convergents([3]), (3,);

    is-deeply convergents([3, 5]), (3.0, 16/5);

    is-deeply
        convergents(1 xx 5),
        (1.0, 2.0, <3/2>, <5/3>, <8/5>),
        'Fibonacci approximations of ϕ';

    is-deeply
            convergents(π, 10),
            (3.0, <22/7>, <333/106>, <355/113>, <103993/33102>, <104348/33215>, <208341/66317>, <312689/99532>, <833719/265381>, <1146408/364913>),
            'π, 10 terms';

    is-deeply
            convergents(e, 10),
            (2.0, 3.0, <8/3>, 2.75, <19/7>, 2.71875, <106/39>, <193/71>, <1264/465>, <1457/536>),
            'e, 10 terms';


}, 'convergents';

done-testing;